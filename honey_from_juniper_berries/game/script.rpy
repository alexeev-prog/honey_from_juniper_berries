# The script of the game goes in this file.

# Declare characters used by this game. The color argument colorizes the
# name of the character.

define gg = Character("Ты")
define narr = nvl_narrator
define boss = Character("Начальник", color="#e92b2b")
define herald = Character("Глашатай", color="#0ca18d")
define p = Character("Зеваки из толпы", color="#5e9159")
define guard = Character("Стражник", color="#1e71be")

define audio.gamemusic = "audio/background-music-0.mp3"

default player_name = ""

### Начальная сцена на площади города Винмор
label vinmor_town_first_scene:
    play music "ost/town_scene_vinmor_1.ogg"

    "Солнце ослепительно сияло. [player_name] даже машинально закрыл лицо руками: уж больно оно показалось ярким после темного подвала."
    "Перед ним открылась площадь Винмора - на сцене уже заканчивал речь глашатай и уже спускался, немного кряхтя."
    "Народ начал медленно рассасываться, но разговоры не стихали. Вон у лавки Миры несколько мужиков отчаянно обсуждают речь глашатая."
    "С другой стороны рынка сидел сгорбленный старик, говоря что все это \"чушь несусветная, князю лишь бы денег заработать. да не отдаст он 500 крон, кинут в темницу и все.\""
    "[player_name], просачиваясь сквозь толпу, подошел к глашатаю и смог разглядеть его поближе. Модный ярко-красный пиджак, плащ, шикарные штаны, белые носки и заостренные туфли."
    "Его не менее эксцентричная шляпа с огромным пером повернулась в его сторону. Его идеальные тонкие и длинные усы усмехнулись тебе, а лицо расплылось в слащавой улыбке."
    "Но как только он увидел тебя, брезгливо сморщил нос. Но в остальном пытался держаться культурно."
    herald "О! Первый герой, готовый стать героем Винмора и вернуть рецепт! Во славу нашего великого княжества!"
    gg "Да, я хотел бы..."
    herald "Нет-нет! Не надо слов! Князь уже распорядился. Твоя задача - найти бедного Вилода."
    "Тут глашатай наигранно вытер платком с золотой обшивкой несуществующею слезу."
    herald 'Вы, ха-ха, знаете где его искать то? Говорят ведьма на окраине деревни видела его. Ну и карга, эта ваша Милада, чуть не прогнала. Еще и живет в грязи...'
    herald "Мерзотная старуха. Не знаю, чего вы ее еще не сожгли. Ночью придёт и перережет всем горло!"
    "Глашатай поморщился когда произносил имя старухи, и потом переигранно отобразил как спящему крестьянину режут горло."
    herald "А, ты еще здесь. Чего хотел? Мне тут надо искать место, в вашем богом забытом городишке."
    gg 'Кхм, я хотел бы стать добровольцем.'
    herald "И обязательно надо было прийти и отвлечь! Я занят - говорю королевский указ - тут нужно тонкое состояние души!"

    menu:
        "Вы немного замешкались..."

        "Извиняюсь, ваше преподобие, что вы так заняты и смею вас отвлекать. Но я хочу отправиться искать Вилода. Что за Милада? Где ее искать?":
            "[player_name] наигранно кланяется, в ответ на это глашатай закатывает глаза."
            herald "Ах, да, Милада... Да там, та ведьма, у леса этого вашего, как его там. Медолесье, Чернолесье... А теперь с глаз моих долой!"

        "А что мне делать? Регистрироваться у вас? А где получить награду?":
            herald "Награда? Регистрация? Срок неделя - принесешь князю рецепт или Вилода, и тогда поговорим."
            "Глашатай, сладко мурлыкая, шепчет:"
            herald 'Но если вы погибните... Земля вам подушкой. А если будет несколько - то князь решит какой рецепт лучше... хи-хи-хи...'
            "Вы разворачиваетесь и уходите поскорее от этого заносчивого нарцисса."
            "Вы же копаетесь в памяти города, и идете к воротам. Вроде-бы там живет как раз та карга Милада, может она что знает..."

        "А вы можете дать мне немного крон? Дорога опасная.":
            herald "Вы здесь все нищие что-ли? Ладно, держи свои 5 крон. Пропьешь - я тебе устрою милость государскую."
            "Видно, что глашатай взял себе долю. Но спорить с ним - себе дороже. [player_name] кладет монеты себе в карман."
            "[player_name], уходит, с приподнятым настроением."
            "Вы копаетесь в памяти города, и идете к воротам. Вроде-бы там живет как раз та карга Милада, может она что знает..."

        "*Уйти молча*":
            "[player_name] отварачивается и уходит. Глашатай смотрит ему в след, и мигом забывает про него, справшивая у местного где можно остановиться:"
            p "Ээ... Ну можете там, в нашей таверне \"Мёд Винмора\". Вы это, если че, на вывеску не смотрите, мёда там нет. И там, эээ, ну и есть даже дорогая комната."
            herald "Кхм... Спасибо. Не трогайте меня своими руками только!"
            "Глашатай мигом уходит, и вы слышите стук его туфель по тротуару."
            "Вы же копаетесь в памяти города, и идете к воротам. Вроде-бы там живет как раз та карга Милада, может она что знает..."

    "Вы решаете покопаться в памяти, и вспомнили что в последний раз Вилод как раз уходил в Медолесье, нынешнее Чернолесье, искать ингридиенты для \"лучшего мёда\", как он выразился."

    # план: дальше игрок у ворот, он может пойти к реке, но его там вернут обратно сюда, как там заливы и нельзя будет плыть. остается лес и милада.
    # todo
    return

### Точка входа
label start:
    play music gamemusic

    scene bg_basement with dissolve

    $ player_name = renpy.input("Введите ваше имя:").strip()

    while player_name == "" or len(player_name) > 16:
        if player_name == "":
            $ renpy.notify("Имя не может быть пустым. Пожалуйста, введите имя.")
        elif len(player_name) > 16:
            $ renpy.notify("Имя не может превышать 16 символов. Пожалуйста, введите имя.")

        $ player_name = renpy.input("Введите ваше имя:").strip()

    narr """[player_name] сидел в душном подвале конторы в городе Винмор, перебирая кипы пожелтевших (и не очень) бумаг. Счёты в его руках разбивали тишину своим монотонным пощелкиванием.
    Каждый новый день был копией предыдущего - бесконечные колонки цифр, претензии ростовщиков и кредиторов, отчеты и бесконечные расписки, документы и просто бумага.
{p}
    Мысли о старом Вилоде и его волшебном мёде из можжевеловых ягод были единственным светом в этой кромешной тьме.
    [player_name] отчетливо помнил тот роковой день двадцать лет назад, когда они вместе везли последнюю партию мёда через Медолесье.
    Тогда ещё лес жил полной жизнью - деревья шептались на ветру, река звенела как хрусталь, а воздух был напоен ароматами дикого мёда и цветущих трав.
    """ with dissolve

    nvl clear

    narr """Но Вилод исчез. Мёд быстро иссяк, а рецепта он не оставил. Фестивали солнцестояния начали отменять, напряжение росло. Медолесье стала поглащать тьма междоусобных войн между Винмором и соседним Тельмором, превратив его в Чернолесье.
    С тех пор люди не слышали звонкие голоса детей, пение птиц и шелест листвы - только мрачная, даже пугающая растительность.
{p}
    [player_name] надеялся что вот-вот Вилод вернется. Но день за днем, декада за декадой, никто не приходил. Он оказался в долговой яме глубиной в сто золотых крон.
    Даже в те времена такую сумму было тяжело заработать, а сейчас подавно. Начальник грозил отобрать имущество и дом.
{p}
    Теперь вместо ароматных медовых троп он блуждал в лабиринтах бухгалтерских отчётов, а вместо душистого ветра Медолесья вдыхал запах плесени и пыли.
    Его пальцы машинально перебирали костяшки счёт, а глаза безжизненно скользили по колонкам цифр, не видя в них ни смысла, ни надежды.
{p}
    [player_name] ждал чуда, не зная какого. Он просто мечтал чтобы было как раньше...
{p}
    Вот так и проходит жизнь: в ожидании чуда, которое, кажется, никогда не случится.
    """ with dissolve

    nvl clear
    with dissolve

    "Потертый камзол, на локтях лоснящийся от долгого сидения за столом. Сапоги, стоптанные не о лесные тропы, а о каменные плиты конторы - не ту жизнь хотел [player_name]."

    gg "Опять недоимка по курятнику! Черт, да когда эти крестьяне поймут что я просто считаю!"
    gg "Еще и этот Гусо требует быстрее посчитать сумму за его кобыл. Мда..."
    "Холодная влажность камней подвала пробиралась сквозь тонкую подошву сапог. [player_name] поёжился."

    "[player_name] мимолетно взглянул по пустую склянку мёда на столе. Он провел пальцем по затертой надписи \"Мёд из можжевеловых ягод от Вилода\". Он ее берет ослабшими руками."
    "Он держит ее словно жаждующий бутылку воды, как бедняк держит мешочек монет, а муж - руку больной жены."
    "[player_name] вздохнул и положил ее рядом с собой, как будто она могла ему помочь."

    gg "Эх, Вилод... Помнится, ты говорил что настоящий мёд варится из свободы, а не из купленных ингридиентов. Варится на природе, а не в затлухом подвале..."
    "Сладко-гнилой запах досок, едкий запах дешевых чернил ударили в нос."
    gg "А теперь я здесь. Сижу в сыром подвале, утяжеленный долгами..."
    gg "Испить бы твоего доброго мёда... Фирменного, с можжевеловыми ягодами..."
    gg "Интересно, куда же все таки пропал Вилод. Ушел собирать ингридиенты и не вернулся..."
    "Как будто-бы в напоминание показалось что из той склянки мёда еще пахнет свежим можжевельником, детским смехом и доброй улыбкой Вилода..."
    gg "А то пить нечего уже. То пойло от Карла даже свиньям не подойдет. Вкус ужасен, спасибо что не воняет."
    "В комнату зашел ваш начальник. Чванливый, толстый лысый мужчина в сюртуке."
    boss "Эй, [player_name], где ты там возишься? У нас еще дела по подсчетам. Ты не забыл что должен мне?"
    "[player_name] не отрываясь от бумаг, буркнул: \"Да не забыл...\" – и с раздражением стер кляксу, размазав ее еще больше."
    gg "Мне надо... Передохнуть... Начальник, можно отгу-..."
    boss "НЕТ! Какой еще отгул? У нас тут дела рекой текут - расписки за службу военную пришли. Считать кто будет?"
    "[player_name] поморщился и начал кивать головой"
    gg "Да, да, будет сделано завтра..."
    boss "Хватит завтраками кормить. Даю тебе 3 дня - либо золото либо чтобы вся работа была сделана. Иначе вылетешь отсюда как свинья из курятника."
    boss "Чтобы завтра были деньги! Это приказ! Выброшу на улицу твою наглую морду! Поганье, только и думает что нажиться на таких добряках как я..."
    "Начальник, громко ругая всех и вся, уходит."
    "[player_name] со злобным взглядом посмотрел на бумаги. Расписки с жирными отпечатками пальцев неграмотных крестьян, казенные бланки с гербом княжества, заляпанные салом."
    "Скрипучий качающийся стол и дергающееся пламя свечи, освещающее тусклое помещение. В углу стоял сломанный стул, на нем ящик с пустыми бутылками сидра..."
    "На стене небольшая полка, с несколькими потрепанными папками, книгами. Гроссбух с оторваным корежком."
    "[player_name] случайно дернул рукой и поднял пыль, закашлившись, он с ужасением упал на стул, который предательски скрипнул."
    "Не так он планировал провести жизнь. Как будто в насмешку пробежали радостные дети на улице."
    "Кроме свечи источником света было открытое окно наверху, открывающее вид на каменную площадь."
    "[player_name] стиснул зубы так, что заболела челюсть. Ком подкатил к горлу, но он лишь глубже вжался в спинку стула, глядя вслед уходящему начальнику."

    "Тут из окна звучит голос глашатая князя Миродара..."
    herald "Господа и дамы! Письмо от князя! Его высочество Миродар объявляет о задании!"
    herald "Граждане! Услышьте глас княжеской милости! Пятьсот... да-да, не ослышались!.. пятьсот златых крон ждут героя!"
    gg "Что за? Пятьсот крон? Что там учудил этот хитрец Миродар?"
    "Вы выглядываете в окно и видите что на городской сцене стоит расфуференный глашатай,, зачитывая рукопись."
    herald "Да-да, все верно! Пятьсот золотых за то, что найдете рецепт мёда из можжевеловых ягод или найдете старика Вилода!"
    "Начала стекаться толпа зевак. Кто-то просто слонялся по городу, кто то торговал, а кто-то специально отвлёкся от работы."
    p "Да ну, что за брехня. Миродар совсем спятил... Тьфу!"
    p "Дак он 20 лет назад пропал! Князь решил своих граждан загубить?"
    herald "... и все это к праздинку летнего солценстояния! Фестиваль снова будет проводиться! Гарантируется титул почетного гражданина!"
    "Начали подходить люди всех мастей. Местный шарлотан, коренастая женщина с корзинкой с луком, кузнец с молотом, решивший отвлечься от работы."
    p "Фууу! Какой еще фестиваль, после пропажи его никто не проводил. И тем более он проводился вместе с грязью из Тельмара. Я не буду якшаться с ними!"
    p "Кто там про Тельмар что сказал? У меня там тётка живёт!"
    p "Хе-хе, да тут можно рецепт бурды дать, взять мешок золота и свалить..."
    p "Ловко однако, пятьсот золотых. Эй, глашатай, а срок то маловат будет!"
    herald "Великая дружба с Тельмаром будет восстановлена! Конец вражды, граждане! Успевайте, шанс только один! Князь милостлив сегодня ко всем!"
    p "Тельмар?! Граждани нашего Винмора не будут пить вместе с сбродом из этого города."
    p "А как тельмарцы пройдут? У реки период разливов, получается только через Чернолесьеье... Князь решил их укокошить?"
    p "Че?! Тельмарцы в нашем славном Винморе? Да я их вышибу отсюда своими руками! Свиньи!"
    p "Эй! Глашатай! А что если никто не принесет? Двадцать лет, мы детям такое рассказываем. Вы там совсем ку-ку?"
    herald "Граждане! Успокойтесь! Неужели не найдется героя? Иначе князь может объявить о наборе добровольцев, чтобы пойти в Чернолесье!"
    "Глашатай нахмурился, его слащавая улыбка сменилась ледяной гримасой и гротескной маской гнева. Солнце как будто потускнело, народ теперь пытался немного отойти от сцены..."
    herald 'Конечно же никто не хочет! Пятьсот золотых! На все задание - неделя!'
    p "Ох-ох, может сынка послать? Он любит приключения."
    p "Да ваш проклятый фестиваль лес погубли, ей богу! Олухи, вы еще и город погубите!"
    p "А я знаю! Князя Миродара король прищучил за вражду двух городов в нашем княжестве. Вот и пытается выслужить. Пятьсот золотых для него копейки!"
    guard "А ну замолать! Темница то быстро научит молчать."
    herald "Стража, потише. Он прав - князь волнуется за наши города и наше княжество... Но прав частично, князь волнуется искренне... Ибо сегодня великий день!"
    herald "Именем князя! Именем короля и всего нашего княжества! Спасите же свой город и фестиваль!"
    herald "Напоминаю - через неделю фестиваль. Ждём героев, которые спасут наше княжество Мелодин!"

    "[player_name] медленно опустил перо, чернильная клякса расплылась на отчете о куриных недоимках. Его взгляд уперся в пустую склянку, пальцы бессознательно сжали счеты так, что костяшки затрещали."
    "Пятьсот золотых... Да этого хватит долги раздать и жить припеваючи... Да и неделя вроде не маленький срок..."
    "Но с другой стороны - уж больно странно что князь решил именно сейчас, спустя 20 лет провести фестиваль летнего солнцестояния. Еще и с мёдом из можжевеловых ягод..."
    "Хотя кто то на улице кричал что это король заставил князя восстановить праздник, ведь именно после пропажи Вилода началась вражда между нашим Винмором и Тельмаром через лес."
    "[player_name] встает на небольшой сундук у окна, чтобы выглянуть и подумать..."
    "Всё таки он может просто усердно работать и выплатить все долги. Стабильность иногда лучше неизвестности. Лучше синица в руках, чем журавль в небе."
    "[player_name] буквально разраывается между двумя берегами одной реки под названием жизнь."

    menu:
        "Что же делать?"

        "Остаться в конторе":
            gg "Ну к черту этот приказ. Наверняка найдется какой то лихой герой. Хорошо что это не я, долги сами себя не выплатят."
            gg "Да и найти Вилода или хотя бы рецепт мёда - из разряда \"сходи туда не знаю куда\"."
            "Вы остались в конторе. Постепенно, под ругательство начальника, вы выплатили долг."
            "Проработав остаток жизни в конторе, вы понимаете, что вы ничего по факту то и не успели. Да, деньги есть. Но зачем они сейчас? Вы уже слишком стары..."
            "Никто не знает, что случилось потом. Может быть, [player_name] ушел в мир иной, или ушел гулять по Медолесью, так и не вернувшись..."
            "Может быть, однажды он одумается."
            return

        "Рискнуть":
            play sound "AnimalSFX/cicada.wav"
            "[player_name] решительно откидывает бумаги, чернила льются на то самое дело о недоимке куриц. Он собирает вещи в свою котомку, окидывая взглядом помещение..."
            "Интересно, какое оно оказывается темное и сырое. И ведь [player_name] мог прожить всю жизнь в этом тихом ужасе."
            gg "О, чуть не забыл, склянка мёда!"
            "[player_name] берет пустую бутыль мёда Вилода и кладет в пояс. Зачем? Как знак, помощь в поисках? Или просто для подбадривания?"
            "Бутыль, пустая и легкая, странно отяжелела в его руке. И как будто краски на ней стали ярче. [player_name] на секунду поверил что она сама введет его в приключение."
            "[player_name] выходит, даже выбегает на площадь. Хорошая, небогатая но добротная походная одежда на нем. В сумке немного рациона и карт."
            boss "Эй, [player_name], куда собрался? А отчёты?"
            gg "У меня дела!"
            "Начальник не успел вас схватить и вы успели выйти из подвала. Начальник смотрел с округлыми глазами в вашу сторону. Опомнившись, его лицо побагровело еще больше обычного."
            "Он напыщился так, что пуговицы на его сюртуке напряглись, и пытался угнаться за вами, но спустя несколько ступенек начал задыхаться."
            boss "Куда побежал! Экх... Ты мне должен! Засужу чёрт тебя побери! Дом засужу! Иди сюда! Все... Агрх... Отберу!"
            "Увы, [player_name] уже не слышал его. Герой решил поставить все на кон. Лучше уж чем потерять остаток жизни сидя в гнилой конторе."

    jump vinmor_town_first_scene

    return
